plugins {
    id 'com.github.sherter.google-java-format' version '0.6'
    id 'net.researchgate.release' version '2.4.0'
}

release {
    preTagCommitMessage = 'Updating to release version '
    tagCommitMessage = 'New release: '
    newVersionCommitMessage = 'Creating new SNAPSHOT version '
    git {
        pushToRemote = 'upstream'
    }
}

apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'maven'
apply plugin: 'maven-publish'


repositories {
    mavenCentral()
    jcenter()
}

group = 'com.nordstrom'
archivesBaseName = 'proto2openapi'

description = """Convert protobuf 3 to Open API 3."""

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile 'com.google.guava:guava:21.0'
    compile 'com.squareup.wire:wire-schema:2.2.0'
    compile 'io.swagger.core.v3:swagger-core:2.0.0-rc4'
    compile 'org.projectlombok:lombok:1.16.20'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.4'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
}

checkstyle {
    toolVersion = '8.3'
}

test {
    useJUnitPlatform()
}

// Add Javadoc + sources jars to artifacts list.
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// Run javadoc as part of the `check` task.
check.dependsOn(javadoc)
def pomExtra = {
    scm {
        connection 'scm:git:git@github.com:Nordstrom/proto2openapi.git'
        developerConnection 'scm:git:git@github.com:Nordstrom/proto2openapi.git'
        url 'https://github.com/Nordstrom/proto2openapi'
    }

    licenses {
        license {
            name = 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }

    developers {
        developer {
            id 'andyday'
            name 'Andy Day'
            email 'andy.day@nordstrom.com'
        }
    }
}

publishing {
    publications {
        proto2openapiMaven(MavenPublication) {
            from components.java
            groupId group
            artifactId archivesBaseName
            version version
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().appendNode('description', description)
                asNode().appendNode('name', 'proto2openapi library')
                asNode().appendNode('url', 'https://github.com/Nordstrom/proto2openapi')
                asNode().children().last() + pomExtra
            }
        }
    }
}
